<!DOCTYPE HTML>
<html lang="en-us">
<head>
  <meta charset="UTF-8">
  <title>Sending EMail With VBA - Excel Tools | Excel Tool Set</title>
  <meta name="viewport" content="width=device-width, initial-scale=1, maximum-scale=3, minimum-scale=1">
  
  <meta name="description" content="It is not difficult to add the ability to send email from your application. If all you want to do is send the workbook, with just a subject but no content, you can use ThisWorkbook.SendMail. However, if you want to include text in the body of the message or include additional files as attachments, you need some VBA code. The page describes a function called SendEmail that wraps up the details in a nice, VBA-friendly function.
">
  <meta name="author" content="exceltoolset">
  <meta itemprop="name" content="Sending EMail With VBA - Excel Tools | Excel Tool Set">
  <meta itemprop="description" content="It is not difficult to add the ability to send email from your application. If all you want to do is send the workbook, with just a subject but no content, you can use ThisWorkbook.SendMail. However, if you want to include text in the body of the message or include additional files as attachments, you need some VBA code. The page describes a function called SendEmail that wraps up the details in a nice, VBA-friendly function.
">
  <meta itemprop="image" content="http://www.exceltoolset.com/img/author.jpg">
  
  
  <meta name="twitter:description" content="">
  
  <link rel="shortcut icon" href="http://www.exceltoolset.com/img/favicon.ico"/>
  <link rel="apple-touch-icon" href="http://www.exceltoolset.com/apple-touch-icon.png" />
  <link rel="apple-touch-icon-precomposed" href="http://www.exceltoolset.com/apple-touch-icon.png" />

  
  <link rel="stylesheet" href="http://www.exceltoolset.com/prism/prism.css">
  <script src="http://www.exceltoolset.com/prism/prism.js"></script>

  <link rel="stylesheet" href="http://www.exceltoolset.com/font/hack/css/hack.min.css">
  <link rel="stylesheet" href="http://www.exceltoolset.com/css/style.css">
</head>

<body>
  <header>
    <div>
  
  <div id="textlogo">
    <h1 class="site-name"><a href="http://www.exceltoolset.com/" title="Excel Tools | Excel Tool Set">Excel Tools | Excel Tool Set</a></h1>
    <h2 class="blog-motto"></h2>
  </div>
  <div class="navbar"><a class="navbutton navmobile" href="#" title="menu"></a></div>
  <nav class="animated">
    <ul>
      
      <li>
        
      </li>
    </ul>
  </nav>
</div>

  </header>
  <div id="container">
    <div id="main" class="post" itemscope itemprop="blogPost">
	<article itemprop="articleBody">
    <header class="article-info clearfix">
  <h1 itemprop="name">
      <a href="http://www.exceltoolset.com/sending-email-with-vba/" title="Sending EMail With VBA" itemprop="url">Sending EMail With VBA</a>
  </h1>
  <p class="article-author">By
    
      <a href="" title="exceltoolset">exceltoolset</a>
    
  </p>
  <p class="article-time">
    <time datetime="2014-05-31 01:38:01 &#43;0000 &#43;0000" itemprop="datePublished">
	<div class="article-content">
    
    <p>It is not difficult to add the ability to send email from your application. If all you want to do is send the workbook, with just a subject but no content, you can use ThisWorkbook.SendMail. However, if you want to include text in the body of the message or include additional files as attachments, you need some VBA code. The page describes a function called <strong>SendEmail</strong> that wraps up the details in a nice, VBA-friendly function.</p>

<p>The code is shown below.</p>

<pre class="brush: vb"><code class="language-basic">Function SendEMail(Subject As String, _
        FromAddress As String, _
        ToAddress As String, _
        MailBody As String, _
        SMTP_Server As String, _
        BodyFileName As String, _
        Optional Attachments As Variant = Empty) As Boolean
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' SendEmail Function
' By Chip Pearson, chip@cpearson.com www.cpearson.com 28-June-2012
'
' This function sends an email to the specified user.
' Parameters:
'   Subject:        The subject of the email.
'   FromAddress:    The sender's email address
'   ToAddress:      The recipient's email address or addresses.
'   MailBody:       The body of the email.
'   SMTP_Server:    The SMTP-Server name for outgoing mail.
'   BodyFileName:   A text file containing the body of the email.
'   Attachments     A single file name or an array of file names to
'                   attach to the message. The files must exist.
' Return Value:
'   True if successful.
'   False if failure.
'
' Subject may not be an empty string.
' FromAddress must be a valid email address.
' ToAddress must be a valid email address. To send to multiple recipients,
' use a semi-colon to separate the individual addresses. If there is a
' failure in one address, processing terminates and messages are not
' send to the rest of the recipients.
' If MailBody is vbNullString and BodyFileName is an existing text file, the content
' of the file named by BodyFileName is put into the body of the email. If
' BodyFileName does not exist, the function returns False. The content of
' the message body is created by a line-by-line import from BodyFileName.
' If MailBody is not vbNullString, then BodyFileName is ignored and the body
' is not created from the file.
' SMTP_Server must be a valid accessable SMTP server name.
' If both MailBody and BodyFileName are vbNullString, the mail message is
' sent with no body content.
' Attachments can be either a single file name as a String or an array of
' file names. If an attachment file does not exist, it is skipped but
' does not cause the procedure to terminate.
'
' If you want to send ThisWorkbook as an attachment to the message, use code
' like the following:
'    ThisWorkbook.Save
'    ThisWorkbook.ChangeFileAccess xlReadOnly
'    B = SendEmail( _
'        ... parameters ...
'        Attachments:=ThisWorkbook.FullName)
'    ThisWorkbook.ChangeFileAccess xlReadWrite
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Required References:
' --------------------
'   Microsoft CDO for Windows 2000 Library
'       Typical File Location: C:\Windows\system32\cdosys.dll
'       GUID: {CD000000-8B95-11D1-82DB-00C04FB1625D}
'       Major: 1    Minor: 0
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Dim MailMessage As CDO.Message
Dim N As Long
Dim FNum As Integer
Dim S As String
Dim Body As String
Dim Recips() As String
Dim Recip As String
Dim NRecip As Long

' ensure required parameters are present and valid.
If Len(Trim(Subject)) = 0 Then
    SendEMail = False
    Exit Function
End If

If Len(Trim(FromAddress)) = 0 Then
    SendEMail = False
    Exit Function
End If

If Len(Trim(SMTP_Server)) = 0 Then
    SendEMail = False
    Exit Function
End If

' Clean up the addresses
Recip = Replace(ToAddress, Space(1), vbNullString)
If Right(Recip, 1) = ";" Then
    Recip = Left(Recip, Len(Recip) - 1)
End If
Recips = Split(Recip, ";")


For NRecip = LBound(Recips) To UBound(Recips)
    On Error Resume Next
    ' Create a CDO Message object.
    Set MailMessage = CreateObject("CDO.Message")
    If Err.Number &lt;> 0 Then
        SendEMail = False
        Exit Function
    End If
    Err.Clear
    On Error GoTo 0
    With MailMessage
        .Subject = Subject
        .From = FromAddress
        .To = Recips(NRecip)
        If MailBody &lt;> vbNullString Then
            .TextBody = MailBody
        Else
            If BodyFileName &lt;> vbNullString Then
                If Dir(BodyFileName, vbNormal) &lt;> vbNullString Then
                    ' import the text of the body from file BodyFileName
                    FNum = FreeFile
                    S = vbNullString
                    Body = vbNullString
                    Open BodyFileName For Input Access Read As #FNum
                    Do Until EOF(FNum)
                        Line Input #FNum, S
                        Body = Body & vbNewLine & S
                    Loop
                    Close #FNum
                    .TextBody = Body
                Else
                    ' BodyFileName not found.
                    SendEMail = False
                    Exit Function
                End If
            End If ' MailBody and BodyFileName are both vbNullString.
        End If
        
        If IsArray(Attachments) = True Then
            ' attach all the files in the array.
            For N = LBound(Attachments) To UBound(Attachments)
                ' ensure the attachment file exists and attach it.
                If Attachments(N) &lt;> vbNullString Then
                    If Dir(Attachments(N), vbNormal) &lt;> vbNullString Then
                        .AddAttachment Attachments(N)
                    End If
                End If
            Next N
        Else
            ' ensure the file exists and if so, attach it to the message.
            If Attachments &lt;> vbNullString Then
                If Dir(CStr(Attachments), vbNormal) &lt;> vbNullString Then
                    .AddAttachment Attachments
                End If
            End If
        End If
        With .Configuration.Fields
            ' set up the SMTP configuration
            .Item("http://schemas.microsoft.com/cdo/configuration/sendusing") = 2
            .Item("http://schemas.microsoft.com/cdo/configuration/smtpserver") = SMTP_Server
            .Item("http://schemas.microsoft.com/cdo/configuration/smtpserverport") = 25
            .Update
        End With
        
        On Error Resume Next
        Err.Clear
        ' Send the message
        .Send
        If Err.Number = 0 Then
            SendEMail = True
        Else
            SendEMail = False
            Exit Function
        End If
    End With
Next NRecip
SendEMail = True
End Function
</code>
</pre>

<p>If you want to attach the workbook that contains the code, you need to make the file read-only when you send it and then change access back to read-write. For example,</p>

<pre><code class="language-basic">ThisWorkbook.Save
ThisWorkbook.ChangeFileAccess xlReadOnly
B = SendEmail( _
    ... parameters ...
    Attachments:=ThisWorkbook.FullName)
ThisWorkbook.ChangeFileAccess xlReadWrite
</code></pre>

<p>The function returns True if successful or False if an error occurred.</p>

<p>The code requires a reference to Microsoft CDO for Windows 2000 Library. The typical file location of this file is C:\Windows\system32\cdosys.dll. The GUID of this component is {CD000000-8B95-11D1-82DB-00C04FB1625D}, with Major = 1 and Minor = 0.</p>

	</div>
  <footer class="article-footer clearfix">
  



<div class="article-categories">
  <span></span>
  
  <a class="article-category-link" href="http://www.exceltoolset.com/categories/vba-functions">VBA Functions</a>
  
</div>



  <div class="article-share" id="share">
    <div data-url="http://www.exceltoolset.com/sending-email-with-vba/" data-title="Sending EMail With VBA" data-tsina="" class="share clearfix">
    </div>
  </div>
</footer>

	</article>
  



</div>

    <div class="openaside"><a class="navbutton" href="#" title=""></a></div>
<div id="asidepart">
<div class="closeaside"><a class="closebutton" href="#" title=""></a></div>
<aside class="clearfix">
  

<div class="categorieslist">
  <p class="asidetitle"></p>
  <ul>
    
    <li><a href="http://www.exceltoolset.com/categories/excel-articles" title="excel-articles">excel-articles</a></li>
    
    <li><a href="http://www.exceltoolset.com/categories/excel-tools" title="excel-tools">excel-tools</a></li>
    
    <li><a href="http://www.exceltoolset.com/categories/vba-functions" title="vba-functions">vba-functions</a></li>
    
  </ul>
</div>



  



  
  <div class="archiveslist">
    <p class="asidetitle"></p>
    <ul class="archive-list">
      
  



  

</aside>
</div>

  </div>
  <footer><div id="footer" >
  
  
  <div class="social-font clearfix">
    
    
    
    
    
  </div>
  <p class="copyright"> Â© 2016
    
    <a href="http://www.exceltoolset.com/" target="_blank" title="exceltoolset">exceltoolset</a>
    
  </p>
</div>
</footer>
  <script src="http://www.exceltoolset.com/js/jquery-2.1.0.min.js"></script>
<script type="text/javascript">
done = false;
$(document).ready(function(){
  $('.navbar').click(function(){
    $('header nav').toggleClass('shownav');
  });
  var myWidth = 0;
  function getSize(){
    if( typeof( window.innerWidth ) == 'number' ) {
      myWidth = window.innerWidth;
    } else if( document.documentElement && document.documentElement.clientWidth) {
      myWidth = document.documentElement.clientWidth;
    };
  };
  var m = $('#main'),
      a = $('#asidepart'),
      c = $('.closeaside'),
      o = $('.openaside');
  $(window).resize(function(){
    getSize();
    if (myWidth >= 1024) {
      $('header nav').removeClass('shownav');
    }else
    {
      m.removeClass('moveMain');
      a.css('display', 'block').removeClass('fadeOut');
      o.css('display', 'none');
    }
  });
  c.click(function(){
    a.addClass('fadeOut').css('display', 'none');
    o.css('display', 'block').addClass('fadeIn');
    m.addClass('moveMain');
  });
  o.click(function(){
    o.css('display', 'none').removeClass('beforeFadeIn');
    a.css('display', 'block').removeClass('fadeOut').addClass('fadeIn');
    m.removeClass('moveMain');
  });
  $(window).scroll(function(){
    o.css("top",Math.max(80,260-$(this).scrollTop()));
  });
  $('form.search').on('submit', function (event) {
    if (false === done) {
      event.preventDefault();
      var orgVal = $(this).find('#search').val();
      $(this).find('#search').val('site:http:\/\/www.exceltoolset.com\/ ' + orgVal);
      done = true;
      $(this).submit();
    }
  });
});
</script>






</body>
</html>
